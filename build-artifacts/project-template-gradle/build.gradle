/*
*	Script builds apk in release or debug mode
*	To run: 
*			gradle buildapk -Prelease (release mode)
*			gradle buildapk (debug mode -> default)
* 	Options:
*			-Prelease  //this flag will run build in release mode
*			-PksPath=[path_to_keystore_file]
*			-PksPassword=[password_for_keystore_file]
*			-Palias=[alias_to_use_from_keystore_file]
*			-Ppassword=[password_for_alias]
*
*			-PtargetSdk=[target_sdk]
*			-PbuildToolsVersion=[build_tools_version]
*			-PsupportVersion=[support_version]
*			-PcompileSdk=[compile_sdk_version]

*			-PdontRunSbg=[true/false]
*/


import groovy.io.FileType
import groovy.json.JsonSlurper

import java.nio.file.Files;
import java.nio.file.Paths;
import java.nio.file.StandardOpenOption
import java.util.regex.Pattern;

buildscript {
    repositories {
        jcenter()
    }

	dependencies {
		classpath "com.android.tools.build:gradle:2.2.3"
	}
}

apply plugin: "com.android.application"

def metadataParams = new LinkedList <String> ()
def allJarPaths = new LinkedList <String> ()
def shouldCreatePluginConfigFile = false
def configStage = "\n:config phase: "
def nodeModulesDir = "../../node_modules/"
def flavorNames = new ArrayList<String>()
def originalConfigurationsDir = new File(projectDir, "configurations")
def configurationsDir = new File(projectDir, "build/configurations")
def dependenciesJsonPath = file("dependencies.json")
// TODO: Pete: Build script will not work with previous CLI versions
def nativescriptDependencies = new JsonSlurper().parseText(dependenciesJsonPath.text)

def packageJsonContents = [:]
def excludedDevDependencies = ['**/.bin/**']

def dontRunSbg = project.hasProperty("dontRunSbg");
def asbgProject = project(":asbg")
asbgProject.ext.outDir = new File("$projectDir", "src/main/java")
asbgProject.ext.jsCodeDir = new File("$projectDir", "src/main/assets/app")

def computeCompileSdkVersion = { -> project.hasProperty("compileSdk") ? compileSdk : 23 }
def computeTargetSdkVersion = { -> project.hasProperty("targetSdk") ? targetSdk : 23 }
def computeBuildToolsVersion = { -> project.hasProperty("buildToolsVersion") ? buildToolsVersion : "25.0.2" }

project.ext.selectedBuildType = project.hasProperty("release") ? "release" : "debug"

def renameResultApks = { variant ->
	def name
	variant.outputs.each { output ->
		def apkDirectory = output.packageApplication.outputFile.parentFile
		def abiName = "";
		if (output.getFilter(com.android.build.OutputFile.ABI)) {
			abiName = "-" + output.getFilter(com.android.build.OutputFile.ABI);
		}
		def apkNamePrefix = rootProject.name + "-" + variant.buildType.name + abiName
		name = apkNamePrefix + ".apk"
		output.packageApplication.outputFile = new File(apkDirectory, name);
	}
}

// gets the devDependencies declared in the package.json and excludes them from the build
task getDevDependencies {
	doLast {
		println "$configStage getDevDependencies"
	
		String content = new File("$projectDir/../../package.json").getText("UTF-8")
		def jsonSlurper = new JsonSlurper()
		def packageJsonMap = jsonSlurper.parseText(content)
	
		packageJsonContents = packageJsonMap.devDependencies;
	
		packageJsonContents.each { entry ->
			excludedDevDependencies.add(entry.key + '/platforms/android/**/*.jar')
		}
	}
}
////////////////////////////////////////////////////////////////////////////////////
///////////////////////////// CONFIGURATIONS ///////////////////////////////////////
////////////////////////////////////////////////////////////////////////////////////

android {
	compileSdkVersion computeCompileSdkVersion()
	buildToolsVersion computeBuildToolsVersion()
	
	defaultConfig {
		minSdkVersion 17
		targetSdkVersion computeTargetSdkVersion()
		ndk {
			abiFilters "armeabi-v7a", "x86"
		}
	}
	
	sourceSets.main {
        jniLibs.srcDir "$projectDir/libs/jni"
    }
	
	signingConfigs {
        release {
            if (project.hasProperty("release")) {
				if (project.hasProperty("ksPath") &&
					project.hasProperty("ksPassword") &&
					project.hasProperty("alias") &&
					project.hasProperty("password")) {

					storeFile file(ksPath)
					storePassword ksPassword
					keyAlias alias
					keyPassword password
				}
			}
        }
    }
    buildTypes {
        release {
            signingConfig signingConfigs.release
        }
    }
	
	applicationVariants.all { variant ->
		renameResultApks(variant)
	}
	
	applicationVariants.all { variant ->
		def variantName = variant.name.capitalize()
		def compileSourcesTaskName = "compile${variantName}Sources"
		def compileSourcesTask = project.tasks.findByName(compileSourcesTaskName)

		def generateBuildConfigTask = variant.generateBuildConfig;
		generateBuildConfigTask.finalizedBy(collectAllJars)
		if(!dontRunSbg) {
			collectAllJars.finalizedBy(setProperties)
		}
		
		compileSourcesTask.finalizedBy(buildMetadata)

		// forces packaging of resources and assets AFTER producing metadata
		// Reference: https://github.com/NativeScript/android-runtime/issues/785

		// Ensure metadata has been built and copied in assets before packaging
		variant.outputs.each { output ->
			def abiName = "";
			if (output.getFilter(com.android.build.OutputFile.ABI)) {
				abiName = output.getFilter(com.android.build.OutputFile.ABI)
				def packageTask = project.tasks.findByName("package${output.name}")
				if (packageTask) {
					packageTask.dependsOn(buildMetadata)
				}
			}
		}

		// Compile the Java sources AFTER the Java code-generation step is done
		def compileTask = project.tasks.findByName("compile${variantName}JavaWithJavac")
		if (compileTask) {
			compileTask.dependsOn("asbg:generateBindings")
		}
	}
}

repositories {
    jcenter()
    maven { url 'https://maven.google.com' }

    // used for local *.AAR files
	def pluginDependencies = nativescriptDependencies.collect { "${it.directory}/platforms/android" }
	pluginDependencies.add("libs/runtime-libs")

    flatDir {
    	dirs pluginDependencies
	}
}

dependencies {
	def supportVer = "22.2.0";
	if (project.hasProperty("supportVersion")) {
		supportVer = supportVersion
	}

	compile "com.android.support:support-v4:$supportVer"
	compile "com.android.support:appcompat-v7:$supportVer"
	debugCompile "com.android.support:design:$supportVer"

    // take all jars within the libs dir
	compile fileTree(dir: "$projectDir/libs", include: ["**/*.jar"])

	// take all jars within the node_modules dir
	compile fileTree(dir: nodeModulesDir, include: ["**/platforms/android/**/*.jar"], exclude: excludedDevDependencies)
}

task addNativeScriptRuntimePackageDependency {
	def useV8Symbols = nativescriptDependencies.any {
		def packageJsonPath = file("${it.directory}/package.json");
		def packageJson = new JsonSlurper().parseText(packageJsonPath.text);
		return packageJson.nativescript.useV8Symbols;
	}

	def runtime = useV8Symbols ? "nativescript-regular" : "nativescript-optimized";
	println "\t+adding nativescript runtime package dependency: $runtime"

	project.dependencies.add("compile", [name: runtime, ext: "aar"])
}

task addAarDependenciesFromNativeScriptPlugins {
	nativescriptDependencies.each() { dep ->
		def aarFiles = fileTree(dir: file("${dep.directory}/platforms/android"), include: ["**/*.aar"])
		aarFiles.each { aarFile ->
			def length = aarFile.name.length() - 4
			def fileName = aarFile.name[0..<length]
			println "\t+adding plugin dependency: " + aarFile.getAbsolutePath()
			project.dependencies.add("compile", [name: fileName, ext: "aar"])
		}
	}
}

task applyAppGradleConfiguration {
	description "applies user-defined gradle configuration"

	def pathToAppGradle = "$projectDir/../../app/App_Resources/Android/app.gradle"
	def appGradle = file(pathToAppGradle)
	if(appGradle.exists()) {
		println "\t+applying user-defined configuration from ${appGradle}"
		apply from: pathToAppGradle
	}
}

////////////////////////////////////////////////////////////////////////////////////
///////////////////////////// CONFIGURATION PHASE //////////////////////////////////
////////////////////////////////////////////////////////////////////////////////////

static def updateProductFlavorsContent(flavor, dimensionName, oldContent) {
	def endIndex = oldContent.length() - 1;
		def index = 0;
		def newContent = "";
		def level = -1;
		def dimensionFound = false;

		while(index <= endIndex) {
			if(level == 0 && (oldContent[index] == '"' || oldContent[index] == "'")) {
				def closingQuotes = oldContent.indexOf('"', index + 1);
				if(closingQuotes == -1) {
					closingQuotes = oldContent.indexOf("'", index + 1);
				}

				index = closingQuotes + 1;
				newContent += "\"${flavor}\"";
				continue;
			}

			if(oldContent[index] == "{") {
				level++;
			}

			if(oldContent[index] == "}") {
				level--;
			}

			if(level > 0) {
				if(!dimensionFound && oldContent.indexOf("dimension", index) == index) {
					newContent += "dimension \"${dimensionName}\"";
					dimensionFound = true;
					index += "dimension ".length();
					def openingQuoutes = oldContent.indexOf('"', index);
					if(openingQuoutes == -1) {
						openingQuoutes = oldContent.indexOf("'", index);
					}

					def closingQuotes = oldContent.indexOf('"', openingQuoutes + 1);
					if(closingQuotes == -1) {
						closingQuotes = oldContent.indexOf("'", openingQuoutes + 1);
					}

					index = closingQuotes + 1;
				}
			}

			newContent += oldContent[index];

			index++;
		}

		return newContent;
}

static def createProductFlavorsContent(flavor, dimensionName, includeAndroidContent = true) {
	if (includeAndroidContent)
  	{
    	def content = """
android {
    productFlavors {
        "${flavor}" {
            dimension "${dimensionName}"
        }
    }
}
"""
    	return content;
	}
	else
	{
    	def content = """
    productFlavors {
        "${flavor}" {
            dimension "${dimensionName}"
        }
    }
"""
      return content;
	}
}

def createIncludeFile (filePath, flavor, dimensionName, destination) {
	println "\t + creating include.gradle file for ${filePath}" 
	
	def defaultIncludeFile = new File(destination, "include.gradle")
	defaultIncludeFile.text =  createProductFlavorsContent(flavor, dimensionName);
}

static def sanitizeDimensionName(str) {
	return str.replaceAll(/\W/, "")
}

static def modifyProductFlavorInContent(content, dimension, flavor) {
	def indexStart = content.indexOf("productFlavors");
	def index = indexStart  + "productFlavors".length();
	def indexEnd = -1;
	def nestedOpenBracketsCount = 0;

	while (index < content.length())
	{
		// print content[index];
		if (content[index] == "}")
		{
			nestedOpenBracketsCount--;

			if (nestedOpenBracketsCount == 0)
			{
				indexEnd = index;
				break;
			}
		}
		else if (content[index] == "{")
		{
			nestedOpenBracketsCount++;
		}

		index++;
	}

	if (indexEnd != -1)
    {
		// full content of productFlavors { ... } -> the substring is parenthesis to parenthesis -> { ... }
      def oldProductFlavorsText = content.substring(indexStart, indexEnd + 1);

      def newProductFlavorsContent = updateProductFlavorsContent(flavor, dimension, oldProductFlavorsText);
      
      return content.replace(oldProductFlavorsText, newProductFlavorsContent);
    }
    else
    {
        def androidContentExists = content.indexOf("android {") != -1;
        def newProductFlavorsContent = createProductFlavorsContent(flavor, dimension, !androidContentExists);
        
        if (androidContentExists)
        {
           return content.replace("android {", "android { ${newProductFlavorsContent}");
        }
        else
        {
           return "${newProductFlavorsContent} \t ${content}"
        }
    }
}

// make sure the include.gradle file provided by the user has only allowed characters in dimension attribute and remove any invalid characters if necessary
def updateIncludeGradleFile(targetFile, dimensionName, flavor, destination) {
	def fileEntry = new File(targetFile.getAbsolutePath());
	def content = fileEntry.text;
	def replacedContent = modifyProductFlavorInContent(content, dimensionName, flavor);
	Files.write(Paths.get(destination.toString(), "include.gradle"), replacedContent.getBytes(), StandardOpenOption.CREATE);
}

static def createPluginConfigurationPath(configurationsDir, originalIncludeGradle) {
	return java.nio.file.FileSystems.getDefault().getPath(configurationsDir.getAbsolutePath(), getParentDirStructure(originalIncludeGradle) as String[]);
}

static def getParentDirStructure(includeGradleFile) {
	def parents = []
	def canonicaFile = includeGradleFile.getCanonicalFile();
	def parentDir = canonicaFile.getParentFile()
	def parentDirName = parentDir.name
	parents.add(parentDirName)
	while (true) {
		parentDir = parentDir.getCanonicalFile().getParentFile()
		parentDirName = parentDir.name
		if (parentDirName == "configurations") {
			break
		}

		parents.add(parentDirName)
	}

	return parents.reverse()
}

def renamePluginDirToFlavorName(directory, flavor) {
    def parentName = directory.getName();
    def parentFile = new File("src", parentName);
    if (parentFile.exists())
    {
        def targetDirName = new File("src", flavor);
        println "Renaming plugin directory to flavor name: ${parentFile.getAbsolutePath()} -> ${targetDirName.getAbsolutePath()}";
        parentFile.renameTo(targetDirName);
    }    
}

def flavorNumber = 0

task createDefaultIncludeFiles {
	description "creates default include.gradle files for added plugins IF NECESSARY"
	println "$configStage createDefaultIncludeFiles"
	def fileTree = file(originalConfigurationsDir)

	def allFlavors = new ArrayList<String>()
	nativescriptDependencies.each {
		def androidDir = file("${it.directory}/platforms/android")
		if (!androidDir.exists()) {
			return
		}

		def packageJsonPath = file("${it.directory}/package.json")
		def packageJson = new JsonSlurper().parseText(packageJsonPath.text)
		def pluginName = packageJson.name
		def dimensionName = sanitizeDimensionName(pluginName)
		flavorNames.add('"' + dimensionName + '"')
		def flavor = "F${flavorNumber++}"

		def includeGradleFile = new File(androidDir, "include.gradle")
		def destinationDir = file("${configurationsDir}/${pluginName}/")
		Files.createDirectories(Paths.get(destinationDir.getAbsolutePath()))

		def destinationIncludeGradleFile = file("${configurationsDir}/${pluginName}/include.gradle")
		def content;
		if (includeGradleFile.exists()) {
			println "\t + add include.gradle from ${includeGradleFile}"
			content = modifyProductFlavorInContent(includeGradleFile.text, dimensionName, flavor)
		} else {
			println "\t + creating include.gradle for plugin ${file(it.directory)}"
			content = createProductFlavorsContent(flavor, dimensionName)
		}

		destinationIncludeGradleFile.text = content
	}

	// old logic copying from configurations/ into build/config and changing text content
//	fileTree.listFiles().each { file ->
//		if (file.isDirectory()) {
//			shouldCreatePluginConfigFile = true
//			def hasChildrenDirs = false
//			file.listFiles().each { subFile ->
//				if (subFile.isDirectory()) {
//					hasChildrenDirs = true
//				}
//			}
//
//			// if plugin is scoped - traverse its children directories
//			// e.g. @scope/plugin-with-android-aars
//			if (hasChildrenDirs) {
//				file.listFiles().each { subFile ->
//					if (subFile.isDirectory()) {
//						createIncludeGradleForPlugin(subFile, flavorNumber++, flavorNames, configurationsDir)
//					}
//				}
//			} else {
//				createIncludeGradleForPlugin(file, flavorNumber++, flavorNames, configurationsDir)
//			}
//		}
//	}

	createPluginsConfigFile(flavorNames, configurationsDir)
}

def createIncludeGradleForPlugin(file, flavorNumber, flavorNames, configurationsDir) {
	def parentDir = new File(file.getParent())
	def parentName = parentDir.name
	def dirToRename = file

	if (parentName.indexOf("@") == 0) {
		dirToRename = file(parentName + "_" + file.name)
	}

	def foundIncludeFile = false
	def fileName = file.name
	def dimensionName = sanitizeDimensionName(fileName)
	
	def flavor = "F" + flavorNumber
	println "\t+found plugins: " + fileName
	file.listFiles().each { subFile ->
		if (subFile.name == "include.gradle") {
			foundIncludeFile = true
			def newIncludeFilePath = createPluginConfigurationPath(configurationsDir, subFile)
			Files.createDirectories(newIncludeFilePath)
			updateIncludeGradleFile(subFile, dimensionName, flavor, newIncludeFilePath)
								renamePluginDirToFlavorName(dirToRename, flavor);
		}
	}
	
	flavorNames.add('"' + dimensionName + '"')
	
	if (!foundIncludeFile) {
		def newIncludeFilePath = createPluginConfigurationPath(configurationsDir, file)
		Files.createDirectories(newIncludeFilePath)
		createIncludeFile(file.getAbsolutePath(), destination, flavor, dimensionName)
		renamePluginDirToFlavorName(dirToRename, flavor);
	}
}

def createPluginsConfigFile(flavorNames, configurationsDir) {
	description "creates product flavor config file based on what plugins are added"

	if (!configurationsDir.exists() || flavorNames.isEmpty()) {
		return
	}

	println "\t Creating product flavors include.gradle file in $configurationsDir folder..."

	def flavorsFile = new File(configurationsDir, "include.gradle")
	def flavors = flavorNames.join(", ")
	def content = """
android {
    flavorDimensions ${flavors}
}
"""

	flavorsFile.text = content
}

task pluginExtend {
	if(configurationsDir.exists()) {
	println "$configStage pluginExtend"
		configurationsDir.eachFileRecurse(FileType.FILES) {
			if(it.name == 'include.gradle') {
				println "\t+applying configuration from: " + it
				apply from: it
			}
		}
	}
}

////////////////////////////////////////////////////////////////////////////////////
///////////////////////////// EXECUTUION PHASE /////////////////////////////////////
////////////////////////////////////////////////////////////////////////////////////

task cleanLocalAarFiles(type: Delete) {
	delete fileTree(dir: "$projectDir/libs/aar", include: ["*.aar"])
}

task ensureMetadataOutDir {
	def outputDir = file("$projectDir/metadata/output/assets/metadata")
	outputDir.mkdirs()
}

task collectAllJars {
	description "gathers all paths to jar dependencies before building metadata with them"
	
	def explodedAarDir = project.buildDir.getAbsolutePath() + "/intermediates/exploded-aar/"
	def sdkPath = android.sdkDirectory.getAbsolutePath();
	def androidJar = sdkPath + "/platforms/" + android.compileSdkVersion + "/android.jar"
	
	doFirst {
		configurations.compile.each { File dependencyFile ->
			logger.info("Task: collectAllJars: dependency file: " + dependencyFile.getAbsolutePath())
			allJarPaths.add(dependencyFile.getAbsolutePath())
		}
		
		allJarPaths.add(androidJar);
		
		def ft = fileTree(dir: explodedAarDir, include: "**/*.jar")
		ft.each { currentJarFile ->
			allJarPaths.add(currentJarFile.getAbsolutePath())
		}

		metadataParams.add("metadata-generator.jar");
		metadataParams.add("$projectDir/metadata/output/assets/metadata");
		def jars = new LinkedList<File>()
		for(def i = 0; i < allJarPaths.size(); i++) {
			metadataParams.add(allJarPaths.get(i));
			def f = new File(allJarPaths.get(i))
			if (f.getName().endsWith(".jar")) {
				jars.add(f)
			}
		}
		
		asbgProject.ext.jarFiles = jars
	}
}

task buildMetadata (type: JavaExec) {
	description "builds metadata with provided jar dependencies"
	
	inputs.files(allJarPaths)
	inputs.dir("$buildDir/intermediates/classes")

	outputs.files("metadata/output/assets/metadata/treeNodeStream.dat", "metadata/output/assets/metadata/treeStringsStream.dat", "metadata/output/assets/metadata/treeValueStream.dat")

	doFirst {
		// get compiled classes to pass to metadata generator		
		// these need to be called after the classes have compiled		
		def classesDir = "$buildDir/intermediates/classes"		

		def classesSubDirs = new File(classesDir).listFiles()
		def selectedBuildType = project.ext.selectedBuildType

		for (File subDir: classesSubDirs) {
			if (!subDir.getName().equals(selectedBuildType)) {
				def subDirBuildType = new File(subDir, selectedBuildType)
				if (subDirBuildType.exists()) {
					metadataParams.add(subDirBuildType.getAbsolutePath());
				}
			}
		}

		def classesDirBuildType = new File(classesDir, selectedBuildType)
		if (classesDirBuildType.exists()) {
			metadataParams.add(classesDirBuildType.getAbsolutePath())
		}

		workingDir "build-tools"
		main "-jar"

		logger.info("Task buildMetadata: Call metadata-generator.jar with arguments: " + metadataParams.toString().replaceAll(',', ''))
		args metadataParams.toArray()
	}
	
	doLast {
		copy {
			from "$projectDir/metadata/output/assets/metadata"
			into "$projectDir/src/main/assets/metadata"
		}
	}
}

task generateTypescriptDefinitions (type: JavaExec) {
	def paramz = new ArrayList<String>();
	def includeDirs = ["com.android.support", "/platforms/" + android.compileSdkVersion]

	doFirst {
		delete "build-tools/typings"

		workingDir "build-tools"

		main "-jar"

		paramz.add("dts-generator.jar");
		paramz.add("-input");

		for (String jarPath: project.jarFiles) {
			// don't generate typings for runtime jars and classes
			if (shouldIncludeDirForTypings(jarPath, includeDirs)) {
				paramz.add(jarPath);
			}
		}

		paramz.add("-output");
		paramz.add("typings");

		logger.info("Task generateTypescriptDefinitions: Call dts-generator.jar with arguments: " + paramz.toString().replaceAll(',', ''))
		args paramz.toArray();
	}
}

generateTypescriptDefinitions.onlyIf {
	project.hasProperty("generateTypings") && Boolean.parseBoolean(project.generateTypings)
}

static def shouldIncludeDirForTypings(path, includeDirs) {
	for (String p: includeDirs) {
		if (path.indexOf(p) > -1) {
			return true;
		}
	}

	return false;
}

task copyTypings {
	doLast {
		println "Copied generated typings to application root level. Make sure to import android.d.ts in reference.d.ts"

		copy {
			from "$projectDir/build-tools/typings"
			into "$projectDir/../../"
		}
	}
}

copyTypings.onlyIf { generateTypescriptDefinitions.didWork }

task validateAppIdMatch {
	doLast {
		def packageJsonFile = new File("$projectDir/../../package.json");
		def lineSeparator = System.getProperty("line.separator");

		if (packageJsonFile.exists() && !project.hasProperty("release")) {
			String content = packageJsonFile.getText("UTF-8")
			def jsonSlurper = new JsonSlurper()
			def packageJsonMap = jsonSlurper.parseText(content)

			if (packageJsonMap.nativescript.id != android.defaultConfig.applicationId) {
				def errorMessage = "${lineSeparator}WARNING: The Application identifier is different from the one inside 'package.json' file.$lineSeparator" +
					"NativeScript CLI might not work properly.$lineSeparator" +
					"Update the application identifier in package.json and app.gradle so that they match.";
				logger.error(errorMessage);
			}
		}
	}
}

////////////////////////////////////////////////////////////////////////////////////
////////////////////////////// OPTIONAL TASKS //////////////////////////////////////
////////////////////////////////////////////////////////////////////////////////////
task setProperties {
	project.ext.jarFiles = []
	doLast {
		def list = [];
		allJarPaths.each({f -> 
			if(f.endsWith(".jar")) {
				list.add(f);
			}
		})
		project.jarFiles = list;
	}
} 

setProperties.finalizedBy("asbg:generateBindings", generateTypescriptDefinitions)

////////////////////////////////////////////////////////////////////////////////////
////////////////////////////// EXECUTION ORDER /////////////////////////////////////
////////////////////////////////////////////////////////////////////////////////////

// -- configuration phase
// 1. Copy *.aar dependencies
// 2. Add *.aar dependencies
// 3. create default include files
// 4. create plugins config file
// 5. plugin extend (apply from include files)

// --execution phase

ensureMetadataOutDir.dependsOn(cleanLocalAarFiles)
collectAllJars.dependsOn(ensureMetadataOutDir)
buildMetadata.dependsOn(collectAllJars)
generateTypescriptDefinitions.finalizedBy(copyTypings)

task buildapk {
	//done to build only necessary apk
	if(project.hasProperty("release")) {
		dependsOn "assembleRelease"
	}
	else {
		dependsOn "assembleDebug"
	}
}

//////// custom clean ///////////
task deleteMetadata (type: Delete){
	delete "$projectDir/metadata/output", "$projectDir/src/main/assets/metadata"
}

task deleteFlavors (type: Delete){
	doLast {
		def srcDir = new File("$projectDir/src")
		srcDir.listFiles().each({ f ->
			if(f.getName() != "main" &&
					f.getName() != "debug" &&
					f.getName() != "release")	{
				delete f
			}
		})
	}
}

task deleteGeneratedBindings(type: Delete) {
	delete "$projectDir/src/main/java/com/tns/gen"
}

buildapk.finalizedBy("validateAppIdMatch");
deleteMetadata.dependsOn(":asbg:clean")
deleteFlavors.dependsOn(deleteMetadata)
deleteGeneratedBindings.dependsOn(deleteFlavors)
clean.dependsOn(deleteGeneratedBindings)
