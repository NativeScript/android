/*
*	Packs metadata generator in a .tgz file in ~/dist folder
*	To build .tgz
*			gradlew packmg
*	To build jar
*			gradlew jarmg
*/
apply plugin: "java"

sourceCompatibility = JavaVersion.VERSION_1_8
targetCompatibility = JavaVersion.VERSION_1_8

buildscript {
    repositories {
        google()
        jcenter()
    }

    dependencies {
        classpath 'com.android.tools.build:gradle:3.3.2'
    }
}

sourceSets {
    main {
        java {
            srcDir 'src/src'
        }
    }
}

compileJava {
    options.compilerArgs << "-Xlint:all" << "-Xlint:-options" << "-Werror"
}

compileJava.outputs.dir("$rootDir/dist/classes")

repositories {
    google()
    mavenCentral()
    jcenter()
}

dependencies {
    compile 'org.apache.bcel:bcel:6.2'
    compile group: 'org.jetbrains.kotlinx', name: 'kotlinx-metadata-jvm', version: '0.1.0'
    compile files("./src/libs/dx.jar")
}

task copyNecessaryFiles {
	doLast {
		copy {
			from "$rootDir/helpers"
			into "$rootDir/dist/bin"
		}

		copy {
			from "$rootDir/package.json"
			into "$rootDir/dist"
		}
	}
}

jar {
    manifest {
        attributes("Manifest-Version": "1.0",
                   "Main-Class": "com.telerik.metadata.Generator")
    }

    from {

        configurations.runtime.collect {
            it.isDirectory() ? it : zipTree(it)
        }

        configurations.compile.collect {
            it.isDirectory() ? it : zipTree(it)
        }
    }
}

task copyJarToBuildTools (type: Copy) {
	from "$projectDir/build/libs/android-metadata-generator.jar"
	into "$projectDir/../"
}

jar.finalizedBy(copyJarToBuildTools)



