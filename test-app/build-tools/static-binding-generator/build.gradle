apply plugin: 'java-library'

sourceCompatibility = JavaVersion.VERSION_1_8
targetCompatibility = JavaVersion.VERSION_1_8

// todo: check if still needed
// if(!project.hasProperty("loadedProjectDeps")){
//     Properties projectDeps = new Properties()
//     projectDeps.load(new FileInputStream("$project.rootDir/gradle.properties"))
//     projectDeps.each { prop ->
//         project.ext.set(prop.key, prop.value)
//     }
//     project.ext.loadedProjectDeps = true
// }

repositories {
    google()
    mavenCentral()
}

dependencies {
    api "org.ow2.asm:asm:${ns_default_asm_version}"
    api "org.ow2.asm:asm-util:${ns_default_asm_util_version}"
    api "org.apache.bcel:bcel:${ns_default_bcel_version}"
    api group: 'org.json', name: 'json', version: "${ns_default_json_version}"
    api "commons-io:commons-io:${ns_default_commons_io_version}"
    api "com.google.code.gson:gson:${ns_default_gson_version}"
    api "com.google.googlejavaformat:google-java-format:${ns_default_google_java_format_version}"

    testImplementation "com.google.code.gson:gson:${ns_default_gson_version}"
    testImplementation "junit:junit:${ns_default_junit_version}"
    testImplementation "org.mockito:mockito-core:${ns_default_mockito_core_version}"
}

compileJava {
    if (!findProject(':dts-generator').is(null)) {
        dependsOn ':dts-generator:copyJarToBuildTools'
    }
    options.compilerArgs << "-Xlint:all" << "-Xlint:-options" << "-Werror"
}

task copyJarToBuildTools (type: Copy) {
    inputs.file("$projectDir/build/libs/static-binding-generator.jar")
    from "$projectDir/build/libs/static-binding-generator.jar"
    into "$projectDir/../"
}

jar {

    configurations.implementation.setCanBeResolved(true)
    configurations.runtimeOnly.setCanBeResolved(true)
    configurations.api.setCanBeResolved(true)

    manifest {
        attributes("Manifest-Version": "1.0",
                "Main-Class": "org.nativescript.staticbindinggenerator.Main")
    }
    from {

        configurations.implementation.collect {
            it.isDirectory() ? it : zipTree(it)
        }

        configurations.runtimeOnly.collect {
            it.isDirectory() ? it : zipTree(it)
        }

        configurations.api.collect {
            it.isDirectory() ? it : zipTree(it)
        }
    }

    duplicatesStrategy = 'include'
}

jar.finalizedBy(copyJarToBuildTools)