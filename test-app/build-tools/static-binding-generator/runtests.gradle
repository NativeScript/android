import java.nio.file.Files
import java.nio.file.StandardCopyOption

apply plugin: 'application'
apply plugin: 'java-library'


if(!project.hasProperty("loadedProjectDeps")){
    Properties projectDeps = new Properties()
    projectDeps.load(new FileInputStream("$project.rootDir/gradle.properties"))
    projectDeps.each { prop ->
        project.ext.set(prop.key, prop.value)
    }
    project.ext.loadedProjectDeps = true
}


defaultTasks 'runSbg'

repositories {
    google()
    mavenCentral()
}

dependencies {
    api "org.ow2.asm:asm:${asm_version}"
    api "org.ow2.asm:asm-util:${asm_util_version}"
    api "org.apache.bcel:bcel:${bcel_version}"
    api group: 'org.json', name: 'json', version: "${json_version}"
    api "commons-io:commons-io:${commons_io_version}"
    api "com.google.code.gson:gson:${gson_version}"
    api "com.google.googlejavaformat:google-java-format:${google_java_format_version}"

    testImplementation "com.google.code.gson:gson:${gson_version}"
    testImplementation "junit:junit:${junit_version}"
    testImplementation "org.mockito:mockito-core:${mockito_core_version}"
}

def appRoot = file(project.appRoot).getAbsolutePath()
def generatedJavaClassesRoot = file(project.generatedJavaClassesRoot).getAbsolutePath()

def inputFile = file('../sbg-input-file.txt')
def outputFile = file('../sbg-output-file.txt')
def javaDependenciesFile = file('../sbg-java-dependencies.txt')
def interfaceNamesFile = file('../sbg-interfaces-names.txt')

jar {
    configurations.api.setCanBeResolved(true)
    manifest {
        attributes("Manifest-Version": "1.0",
                "Main-Class": "org.nativescript.staticbindinggenerator.Main")
    }
    from {
        configurations.api.collect {
            it.isDirectory() ? it : zipTree(it)
        }
    }
    duplicatesStrategy = 'include'
}

task copyJar(dependsOn: 'jar') {
    doFirst {
        def source = file("$projectDir/build/libs/static-binding-generator.jar").toPath()
        print source.toFile().exists()
        def dest = file("$projectDir/../static-binding-generator.jar").toPath()
        Files.copy(source, dest, StandardCopyOption.REPLACE_EXISTING)
    }
}

task prepareInputFiles(dependsOn: 'copyJar') {
    doFirst {
        inputFile.write(appRoot)
        outputFile.write(generatedJavaClassesRoot.toString())
        javaDependenciesFile.write('')
        interfaceNamesFile.write('')
    }
}

task runSbg(type: JavaExec, dependsOn: 'prepareInputFiles') {
    classpath = files('../static-binding-generator.jar', '../')
    workingDir = "../"
    main = "org.nativescript.staticbindinggenerator.Main"
}
java {
    sourceCompatibility = JavaVersion.VERSION_1_8
    targetCompatibility = JavaVersion.VERSION_1_8
}